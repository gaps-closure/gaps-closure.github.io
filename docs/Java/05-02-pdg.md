## System Dependency Graph (SDG) **Review: Rob, PSU** {#pdg-appendix}


private thread[SDG sdg] returns [ThreadInstance ti]
  : 'Thread' id=number '{'
      'Entry'   en=number ';'
      'Exit'    ex=number ';'
      'Fork'    fo=mayNegNumber ';'
      'Join'    joins=listOrSingleNumber[sdg] ';'
      'Context' con=context[sdg] ';'
      'Dynamic' dyn=bool ';'
    '}'
    {
      final SDGNode entry = sdg.getNode(en);
      SDGNode exit = null; if (ex != 0) { exit = sdg.getNode(ex); }
      SDGNode fork = null; if (fo != 0) { fork = sdg.getNode(fo); }
      ti = new ThreadInstance(id, entry, exit, fork, joins, con, dyn);
    }
  ;
  
private listOrSingleNumber[SDG sdg] returns [LinkedList<SDGNode> js]
  : joins=mayEmptyNumberList[sdg] { js = joins; }
  | jo=number {
                js = new LinkedList<SDGNode>();
                if (jo != 0) { js.add(sdg.getNode(jo)); }
              }
  ;

private mayEmptyNumberList[SDG sdg] returns [LinkedList<SDGNode> js = new LinkedList<SDGNode>();]
  : 'null'
  | '[' ']'
  | '[' i=number { js.add(sdg.getNode(i)); } (',' i=number { js.add(sdg.getNode(i)); } )* ']'
  ;
  
private mayEmptyStringList returns [LinkedList<String> ss = new LinkedList<String>();]
  : 'null'
  | '[' ']'
  | '[' s=string { ss.add(s); } (',' s=string { ss.add(s); } )* ']'
  ;
  
private context[SDG sdg] returns [LinkedList<SDGNode> cx = new LinkedList<SDGNode>();]
  : 'null'
  | '[' i=mayNegNumber { cx.add(sdg.getNode(i)); } (',' i=mayNegNumber { cx.add(sdg.getNode(i)); } )* ']'
  ;

private sdg_header returns [SDGHeader header]
  : 'SDG' 
    { int version = SDG.DEFAULT_VERSION; }
      ('v' n=number { version = n; })?
    { String name = null; }
      (na=string { name = na; })? 
    { Integer rootId = null; }
      ('root' root=number { rootId = root; })? 
    { header = new SDGHeader(version, name, rootId); }
  ;

private node_list returns [List<SDGNodeStub> list = new LinkedList<SDGNodeStub>();]
  : (n=node { list.add(n); } )*
  ;

private node returns [SDGNodeStub nstub]
  : k=node_kind id=mayNegNumber { nstub = new SDGNodeStub(k, id, defaultSrcPos, defaultBcPos); } 
    '{' 
      node_attributes[nstub] 
      node_edges[nstub]
    '}'
  ;

private node_kind returns [SDGNode.Kind kind]
  : 'NORM' { kind = SDGNode.Kind.NORMAL; }
  | 'PRED' { kind = SDGNode.Kind.PREDICATE; }
  | 'EXPR' { kind = SDGNode.Kind.EXPRESSION; }
  | 'ENTR' { kind = SDGNode.Kind.ENTRY; }
  | 'CALL' { kind = SDGNode.Kind.CALL; }
  | 'ACTI' { kind = SDGNode.Kind.ACTUAL_IN; }
  | 'ACTO' { kind = SDGNode.Kind.ACTUAL_OUT; }
  | 'FRMI' { kind = SDGNode.Kind.FORMAL_IN; }
  | 'FRMO' { kind = SDGNode.Kind.FORMAL_OUT; }
  | 'EXIT' { kind = SDGNode.Kind.EXIT; }
  | 'SYNC' { kind = SDGNode.Kind.SYNCHRONIZATION; }
  | 'FOLD' { kind = SDGNode.Kind.FOLDED; }
  ;

private node_attributes[SDGNodeStub node]
  : (node_attr[node] ';')*
  ;

private node_attr[SDGNodeStub node]
  : 'S' spos=node_source    { node.spos = spos; defaultSrcPos = spos; } // sourcecode position
  | 'B' bpos=node_bytecode  { node.bpos = bpos; defaultBcPos = bpos; }  // bytecode position
  | 'U' number                                            // deprecated 'unit' id
  | 'P' procId=number       { node.procId = procId; }     // procedure id
  | 'O' op=node_oper        { node.op = op; }             // operation
  | 'V' val=string          { node.val = val; }           // value
  | 'T' type=string         { node.type = type; }         // type
  | 'Z' tn=may_neg_num_set  { node.threadNums = tn; }     // thread numbers
  | 'N'                     { node.nonTerm = true; }      // no termination guaranteed (termination sensitive ana)
  | 'C' cl=string           { node.classLoader = cl; }    // class loader name
  | 'A' al=pos_num_set      { node.allocSites = al; }     // possible allocation sites (node ids of 'new')
  | 'D' ds=pos_num_set      { node.aliasDataSrc = ds; }    // definitve data sources for this value
  | 'U' uct=string          { node.unresolvedCallTarget = uct; } // signature of call target if call target is a native method
  | 'LD' ldefs=mayEmptyStringList {
                              node.localDefNames = ldefs;
                            } // names of local variables defined
  | 'LU' luses=mayEmptyStringList {
                              node.localUseNames = ldefs;
                            }; // names of local variables used

private pos_num_set returns [TIntSet nums = new TIntHashSet();]
  : n=number { nums.add(n); } (',' n2=number { nums.add(n2); } )*
  ;

private may_neg_num_set returns [TIntSet nums = new TIntHashSet();]
  : n=mayNegNumber { nums.add(n); } (',' n2=mayNegNumber { nums.add(n2); } )*
  ;

private node_source returns [SourceLocation spos]
  : filename=string ':' startRow=number ',' startColumn=number '-' endRow=number ',' endColumn=number
      { spos = spos = SourceLocation.getLocation(filename, startRow, startColumn, endRow, endColumn); }
  ;
  
private node_bytecode returns [ByteCodePos bpos]
  : name=string ':' index=mayNegNumber { bpos = new ByteCodePos(name, index); }
  ;

private node_oper returns [SDGNode.Operation op]
  : 'empty'          { op = SDGNode.Operation.EMPTY; }
  | 'intconst'       { op = SDGNode.Operation.INT_CONST; }
  | 'floatconst'     { op = SDGNode.Operation.FLOAT_CONST; }
  | 'charconst'      { op = SDGNode.Operation.CHAR_CONST; }
  | 'stringconst'    { op = SDGNode.Operation.STRING_CONST; }
  | 'functionconst'  { op = SDGNode.Operation.FUNCTION_CONST; }
  | 'shortcut'       { op = SDGNode.Operation.SHORTCUT; }
  | 'question'       { op = SDGNode.Operation.QUESTION; }
  | 'binary'         { op = SDGNode.Operation.BINARY; }
  | 'unary'          { op = SDGNode.Operation.UNARY; }
  | 'derefer'        { op = SDGNode.Operation.DEREFER; }
  | 'refer'          { op = SDGNode.Operation.REFER; }
  | 'array'          { op = SDGNode.Operation.ARRAY; }
  | 'select'         { op = SDGNode.Operation.SELECT; }
  | 'reference'      { op = SDGNode.Operation.REFERENCE; }
  | 'declaration'    { op = SDGNode.Operation.DECLARATION; }
  | 'modify'         { op = SDGNode.Operation.MODIFY; }
  | 'modassign'      { op = SDGNode.Operation.MODASSIGN; }
  | 'assign'         { op = SDGNode.Operation.ASSIGN; }
  | 'IF'             { op = SDGNode.Operation.IF; }
  | 'loop'           { op = SDGNode.Operation.LOOP; }
  | 'jump'           { op = SDGNode.Operation.JUMP; }
  | 'compound'       { op = SDGNode.Operation.COMPOUND; }
  | 'call'           { op = SDGNode.Operation.CALL; }
  | 'entry'          { op = SDGNode.Operation.ENTRY; }
  | 'exit'           { op = SDGNode.Operation.EXIT; }
  | 'form-in'        { op = SDGNode.Operation.FORMAL_IN; }
  | 'form-ellip'     { op = SDGNode.Operation.FORMAL_ELLIP; }
  | 'form-out'       { op = SDGNode.Operation.FORMAL_OUT; }
  | 'act-in'         { op = SDGNode.Operation.ACTUAL_IN; }
  | 'act-out'        { op = SDGNode.Operation.ACTUAL_OUT; }
  | 'monitor'        { op = SDGNode.Operation.MONITOR; }
  ;

private node_edges[SDGNodeStub node]
  : (e=edge ';' { node.edges.add(e); })*
  ;

private edge returns [SDGEdgeStub estub]
  : k=edge_kind nr=number { estub = new SDGEdgeStub(k, nr); } (':' label=string { estub.label = label; } )?
  ;

private edge_kind returns [SDGEdge.Kind kind]
// data dependencies
  : 'DD' { kind = SDGEdge.Kind.DATA_DEP; }            // data dependencies between local variables
  | 'DH' { kind = SDGEdge.Kind.DATA_HEAP; }           // data dependencies between field accesses
  | 'DA' { kind = SDGEdge.Kind.DATA_ALIAS; }          // data dependencies between aliasing fields accesses
// control dependencies
  | 'CD' { kind = SDGEdge.Kind.CONTROL_DEP_COND; }    // control dependencies between statements
  | 'CE' { kind = SDGEdge.Kind.CONTROL_DEP_EXPR; }    // control dependencies between nodes that correspond to the same statement
  | 'UN' { kind = SDGEdge.Kind.CONTROL_DEP_UNCOND; }  // unconditional control dependencies
// control flow
  | 'CF' { kind = SDGEdge.Kind.CONTROL_FLOW; }        // control flow between statements
  | 'NF' { kind = SDGEdge.Kind.NO_FLOW; }             // control flow that is actually not possible (dead code)
  | 'RF' { kind = SDGEdge.Kind.RETURN; }              // control flow from method exit to call site
// method call related
  | 'CC' { kind = SDGEdge.Kind.CONTROL_DEP_CALL; }
  | 'CL' { kind = SDGEdge.Kind.CALL; }
  | 'PI' { kind = SDGEdge.Kind.PARAMETER_IN; }
  | 'PO' { kind = SDGEdge.Kind.PARAMETER_OUT; }
// summary edges
  | 'SU' { kind = SDGEdge.Kind.SUMMARY; }
  | 'SH' { kind = SDGEdge.Kind.SUMMARY_NO_ALIAS; }
  | 'SF' { kind = SDGEdge.Kind.SUMMARY_DATA; }
// method interface structure
  | 'PS' { kind = SDGEdge.Kind.PARAMETER_STRUCTURE; }
  | 'PE' { kind = SDGEdge.Kind.PARAMETER_EQUIVALENCE; }
// thread/concurrency related edges
  | 'FORK' { kind = SDGEdge.Kind.FORK; }
  | 'FORK_IN' { kind = SDGEdge.Kind.FORK_IN; }
  | 'FORK_OUT' { kind = SDGEdge.Kind.FORK_OUT; }
  | 'JOIN' { kind = SDGEdge.Kind.JOIN; }
  | 'ID' { kind = SDGEdge.Kind.INTERFERENCE; }
  | 'IW' { kind = SDGEdge.Kind.INTERFERENCE_WRITE; }
  | 'SD' { kind = SDGEdge.Kind.SYNCHRONIZATION; }
// general helper edges
  | 'HE' { kind = SDGEdge.Kind.HELP; }
  | 'FD' { kind = SDGEdge.Kind.FOLDED; }
  | 'FI' { kind = SDGEdge.Kind.FOLD_INCLUDE; }
// deprecated edges
  | 'RY' { kind = SDGEdge.Kind.READY_DEP; }
  | 'JF' { kind = SDGEdge.Kind.JUMP_FLOW; }
  | 'SP' { kind = SDGEdge.Kind.SUMMARY; }
  | 'VD' { kind = SDGEdge.Kind.DATA_DEP_EXPR_VALUE; }
  | 'RD' { kind = SDGEdge.Kind.DATA_DEP_EXPR_REFERENCE; }
  | 'JD' { kind = SDGEdge.Kind.JUMP_DEP; }


  ## Limitations
  ...